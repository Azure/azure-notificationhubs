apply plugin: 'com.android.application'

def secretsProperties = new Properties()
def secretsPropertiesFile = rootProject.file("secrets.properties")
if (secretsPropertiesFile.exists()) {
    secretsProperties.load(new FileInputStream(secretsPropertiesFile))
}

def googleServicesFile = rootProject.file("app/google-services.json")
if (!googleServicesFile.exists()) {
    googleServicesFile.write(System.getenv("GOOGLE_SERVICES_JSON"))
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.microsoft.notification_hubs_test_app"
        minSdkVersion 27
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "senderId", System.getenv("APP_SENDER_ID") ?: secretsProperties["APP_SENDER_ID"])
        buildConfigField("String", "hubName", System.getenv("APP_HUB_NAME") ?: secretsProperties["APP_HUB_NAME"])
        buildConfigField("String", "hubListenConnectionString", System.getenv("APP_NH_CONNECTION_STRING") ?: secretsProperties["APP_NH_CONNECTION_STRING"])

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compile 'com.google.android.gms:play-services-gcm:16.0.0'
    compile 'com.microsoft.azure:notification-hubs-android-sdk:0.5@aar'
    compile 'com.microsoft.azure:azure-notifications-handler:1.0.1@aar'

}

repositories {
    maven {
        url "http://dl.bintray.com/microsoftazuremobile/SDK"
    }
}

apply plugin: 'com.google.gms.google-services'
