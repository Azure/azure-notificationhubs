apply plugin: 'com.android.library'
apply plugin: 'maven'

def version = 0.6
def PUBLISH_ARTIFACT_ID = 'notification-hubs-android-sdk'
def groupId = 'com.microsoft.azure'

def BINTRAY_USERNAME = "$System.env.BINTRAY_USERNAME"
def BINTRAY_APIKEY = "$System.env.BINTRAY_APIKEY"

def isCI = project.hasProperty('isCI') ? Boolean.valueOf(isCI) : false

android {
    compileSdkVersion 21
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
}



task clearJar(type: Delete) {
    delete 'build/release/' + PUBLISH_ARTIFACT_ID + '-' + version + '.jar'
}

// step 1
task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/release/')
    include('classes.jar')
    rename ('classes.jar', PUBLISH_ARTIFACT_ID + '-' +  version + '.jar')
}

makeJar.dependsOn(clearJar, build)


// step 2
uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = PUBLISH_ARTIFACT_ID
        pom.version = version
        pom.project {
            packaging 'aar'
            name 'Notification Hub'
            description 'NotificationHub Android SDK'
            url 'https://github.com/Azure/azure-notificationhubs'
        }
        repository (
            url: isCI ? '$System.env.BUILD_ARTIFACTSTAGINGDIRECTORY' : 'https://api.bintray.com/maven/microsoftazuremobile/SDK/Notification-Hubs-Android-SDK'
        ) {
            authentication(
                userName: BINTRAY_USERNAME,
                password: BINTRAY_APIKEY
            )
        }
    }
}


task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
    archives file : file('build/release/' + PUBLISH_ARTIFACT_ID + '-' + version + '.jar')
}